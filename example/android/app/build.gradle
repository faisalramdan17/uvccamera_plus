plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def getJksFile = {
    def value = System.getenv("ANDROID_JKS_FILE")
    if (value != null && !value.isEmpty()) {
        return value
    }
    return null
}
def jksFile = getJksFile()

def getJksPassword = {
    def value = System.getenv("ANDROID_JKS_PASSWORD")
    if (value != null && !value.isEmpty()) {
        return value
    }
    return null
}
def jksPassword = getJksPassword()

android {
    namespace = "org.uvccamera.flutter_example"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "org.uvccamera.flutter_example"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        if (jksFile != null && jksPassword != null) {
            distribution {
                storeFile file(jksFile)
                storePassword jksPassword
                keyAlias "distribution"
                keyPassword jksPassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig = jksFile != null && jksPassword != null
                    ? signingConfigs.distribution
                    : signingConfigs.debug
        }
    }
}

flutter {
    source = "../.."
}
